plugins {
    id 'application'
    id 'net.nemerosa.versioning' version '2.8.2'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {

    implementation 'com.discord4j:discord4j-core:3.1.5'
    implementation 'software.amazon.awssdk:s3:2.16.87'
    implementation 'software.amazon.awssdk:sqs:2.16.87'
    
    implementation group: 'org.tinylog', name: 'tinylog-api', version: '2.2.1'
    implementation group: 'org.slf4j',   name: 'slf4j-api', version: '1.7.30'
    implementation group: 'org.tinylog', name: 'slf4j-tinylog', version: '2.2.1'
    
    runtimeOnly group: 'org.tinylog', name: 'tinylog-impl', version: '2.2.1'
    
    testImplementation 'junit:junit:4.13'
}

application {
    mainClass = 'eu.apuseni.discbot.App'
    applicationDefaultJvmArgs = ['-Xmx256M']
}

version=versioning.info.display
group = 'eu.apuseni.aha'

jar {
  manifest {
    attributes(
      'Main-Class'     : application.mainClass,
      'Class-Path'     : configurations.runtimeClasspath.files.collect { it.getName() }.join(' '),
      'Built-By'       : System.properties['user.name'],
      'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss.SSSZ").format(new Date()),
      'Build-Revision' : versioning.info.build,
      'Build-Version'  : versioning.info.display,
      'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
      'Build-Os'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
      'Created-By'     : "Gradle ${gradle.gradleVersion}"
    )
  }
}